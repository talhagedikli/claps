// 2021-09-25 17:48:56
#event properties (no comments/etc. here are saved)
parent_index = -1;
persistent = true;
uses_physics = false;

#event create
//width and height 480*270
defWidth		=	1920/3;
defHeight		=	1080/3;
windowScale		=	2;

following		= noone;

//spd variables
followSpd		= 0.1;

//cam width and height
viewWidth		= defWidth;
viewHeight		= defHeight;

//first set to default
camW = viewWidth;
camH = viewHeight;

camX = 0;
camY = 0;

//to switch target
newW = 0;
newH = 0;

//camera target
if (instance_exists(following)) 
{
	targetX	= following.x - camW/2;
	targetY	= following.y - camH/2;
}

//set window size
window_set_size(viewWidth * windowScale, viewHeight * windowScale);
alarm[0] = 1;

//re-set surface and gui 
surface_resize(application_surface, viewWidth * windowScale, viewHeight * windowScale);
// display_set_gui_size(viewWidth * windowScale, viewHeight * windowScale);

//shake
shake			= false;
shake_time		= 0;
shake_magnitude = 0;
shake_fade		= 0;

// Methods
/// @func applyScreenShake()
applyScreenShake = function () 
{
	if (shake)
	{
		//reduce shake time by 1(every step)
		shake_time -= 1;
			
		//calculate shake magnitude
		var _xval = random_range(-shake_magnitude, shake_magnitude); 
		var _yval = random_range(-shake_magnitude, shake_magnitude);
			
		//apply the shake
		camX += _xval;
		camY += _yval;
			
		if (shake_time <= 0) 
		{
			//if shake time is zero, shake fade
			shake_magnitude -= shake_fade; 

			if (shake_magnitude <= 0) 
			{
				//if shake fade is zero, turn shake of
			    shake = false; 
			} 
		}
	}
	
}

/// @func updateCameraSize()
updateCameraSize = function (_w, _h) 
{
	camW = flerp(camW, _w, zoomSpd);
	camH = flerp(camH, _h, zoomSpd);
}

state = new SnowState("normal");
state.add("normal", {
	step: function()
	{
		var xTo, yTo;
		if (instance_exists(following))
		{
			xTo = round(following.x) - (camW / 2);
			yTo = round(following.y) - (camH / 2);
		}
		else
		{
			xTo = x;
			yTo = y;
		}
		
		//camX = abs(difX) < EPSILON ? targetX : lerp(camX, targetX, followSpd);
		// camX = 0;
		// camY = 0;
		camX = flerp(camX, xTo, followSpd);
		camY = flerp(camY, yTo, followSpd);
		//camY = abs(difY) < EPSILON ? targetY : lerp(camY, targetY, followSpd);
		applyScreenShake();
		camX = clamp(camX, -shake_magnitude, room_width - camW + shake_magnitude);
		camY = clamp(camY, -shake_magnitude, room_height - camH + shake_magnitude);		
	}
});


#event alarm0
window_center();

#event step
var dw = display_get_width(), dh = display_get_height();
var ww = window_get_width(), wh = window_get_height();
if (window_get_fullscreen())
{
	surface_resize(application_surface, dw, dh);
}
else
{
	surface_resize(application_surface, ww, wh);
}

#event step_end

state.step();

camera_set_view_size(VIEW, camW, camH);
camera_set_view_pos(VIEW, camX, camY);

//track the transition layer
//if (layer_sequence_exists("transitions", global.sequenceLayer))
//{
//	layer_sequence_x(global.sequenceLayer, camX);
//	layer_sequence_y(global.sequenceLayer, camY);
//}	
	
	


#event other_room_start
view_enabled = true;
view_visible[0] = true;



#event draw_gui
/// @description


#event cleanup
/// @description